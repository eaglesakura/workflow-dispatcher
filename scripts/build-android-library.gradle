apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (artifact.dynamic_group_id) {
    group "${artifact.base_group}.${project.name}"
} else {
    group artifact.base_group
}
version = artifact.deploy_version

android {
    compileSdkVersion versions.android_targetSdk
    buildToolsVersion versions.android_buildTools

    // for *.java options
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    defaultConfig {
        minSdkVersion versions.android_minSdk
        targetSdkVersion versions.android_targetSdk
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig false
    }

    sourceSets {
        androidTest.java.srcDirs += ["src/test/java"]
    }

    lintOptions {
        // workaround for "io.grpc/grpc-core/1.16.1"
        disable "InvalidPackage"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true  // support robolectric
            returnDefaultValues = true
            all {
                jvmArgs "-noverify"
            }
        }
    }
}

dependencies {
    /**
     * Kotlin support
     */
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
    api "org.jetbrains.kotlin:kotlin-reflect"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlin_coroutines}"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlin_coroutines}"

    /**
     * for Tests (or Android Tests)
     */
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'io.github.eaglesakura.armyknife-android-junit4:armyknife-android-junit4:1.3.6'
    androidTestImplementation 'io.github.eaglesakura.armyknife-android-junit4:armyknife-android-junit4:1.3.6'
}

apply from: rootProject.file('scripts/formatter.gradle')
